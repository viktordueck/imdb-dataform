config {
    hasOutput: true,
    type: "operations",
    schema: dataform.projectConfig.vars.RAW_DATA,
    tags: ["LOAD_DATA"]
}

LOAD DATA OVERWRITE
  ${self()} ( nconst STRING NOT NULL OPTIONS(description="Alphanumeric unique identifier of the name/person."),
    primary_name STRING OPTIONS(description="Name by which the person is most often credited."),
    birth_year INT64 OPTIONS(description="Birth year in YYYY format."),
    death_year INT64 OPTIONS(description="Death year in YYYY format if applicable."),
    primary_profession STRING OPTIONS(description="The top-3 professions of the person."),
    known_for_titles STRING OPTIONS(description="Titles the person is known for.") )
FROM FILES ( format = "CSV",
    field_delimiter = "\t",
    skip_leading_rows = 1,
    allow_jagged_rows = TRUE,
    quote = "",
    null_marker = "\\N",
    uris = ["gs://${ dataform.projectConfig.vars.LOAD_GCS_BUCKET }/name.basics.tsv"] );
